{ stdenv, automake, writeText, fetchurl, coreutils, utillinux, binutils
, glibc, diffutils, file, psmisc, gnugrep, gnused, gawk, sharutils, zlib
, ghostscript, libtiff, jbigkit, libjpeg, lcms2
, pam, openldap
}:

let
  __systemParts = stdenv.lib.splitString "-" stdenv.system;
  _arch = stdenv.lib.elemAt __systemParts 0;
  _os = stdenv.lib.elemAt __systemParts 1;
  _fontPath = builtins.concatStringsSep ":" [
    "${ghostscript}/share/ghostscript/fonts"
    "${ghostscript}/share/ghostscript/${ghostscript.version}/lib"
    "${ghostscript}/share/ghostscript/${ghostscript.version}/Resource/Init"
    "${ghostscript}/share/ghostscript/${ghostscript.version}/Resource/Font"
    "${ghostscript}/share/fonts/default/ghostscript"
    "${ghostscript}/share/fonts/default/Type1"
    "${ghostscript}/share/fonts/default/TrueType"
  ];

in stdenv.mkDerivation rec {
  name = "hylafaxp-${version}";
  version = "5.5.8";

  src = fetchurl {
    url = mirror://sourceforge/hylafax/hylafax-5.5.8.tar.gz;
    sha256 = "0rnj3a89s2qcmkkgkr7ml7id6ah227rk0p8g59salrxyy0s4wsp0";
  };

  postPatch = ''
    substituteInPlace faxcover/edit-faxcover.sh.in \
      --replace "PATH=/bin" "PATH=${coreutils}/bin:/bin"
    substituteInPlace etc/faxsetup.sh.in \
      --replace "@FILECMD@" "${file}/bin/file" \
      --replace "@FUSER@" "${psmisc}/bin/fuser" \
      --replace "@UUENCODE@" "${sharutils}/bin/uuencode" \
      --replace "PATH=/bin" "PATH=${coreutils}/bin:${gnused}/bin:${gnugrep}/bin:${gawk}/bin:/bin" \
      --replace "/etc/postfix" "/var/postfix/conf" \
      --replace "/usr/lib/aliases" "/etc/aliases"
    substituteInPlace faxmail/mailfax.sh-postfix \
      --replace "/etc/postfix" "/var/postfix/conf"
    substituteInPlace util/faxadduser.c \
      --replace "60002" "65534"
    substituteInPlace hfaxd/manifest.h \
      --replace "60002" "65534"
    substituteInPlace configure \
      --replace "60002" "65534" \
      --replace "/usr/local/share/ghostscript/fonts" "${ghostscript}/share/ghostscript/fonts"
  '';

  hyla_conf = writeText "hyla.conf" ''
    # Warning, FontMap/FontPath was automatically generated by NixOS
    FontMap:   @out@/var/spool/hylafax/etc
    FontPath:  ${_fontPath}
  '';

  setup_cache = writeText "setup.cache" ''
    # Warning, setup.cache was automatically generated by NixOS
    AWK='${gawk}/bin/awk'
    BIN='@out@/bin'
    CAT='${coreutils}/bin/cat'
    CHGRP='${coreutils}/bin/chgrp'
    CHMOD='${coreutils}/bin/chmod'
    CHOWN='${coreutils}/bin/chown'
    CP='${coreutils}/bin/cp'
    DPSRIP='ps2fax.exe'
    ECHO='${coreutils}/bin/echo'
    ENCODING='base64'
    FAXQ_SERVER='yes'
    FILECMD='${file}/bin/file'
    FONTPATH='${_fontPath}'
    FUSER='${psmisc}/bin/fuser'
    GREP='${gnugrep}/bin/grep'
    GSRIP='${ghostscript}/bin/gs'
    HFAXD_SERVER='yes'
    HFAXD_SNPP_SERVER='no'
    IMPRIP='psrip'
    LIBDATA='@out@/etc/hylafax'
    LIBEXEC='@out@/bin'
    LN='${coreutils}/bin/ln'
    MANDIR='@out@/share/man'
    MIMENCODE='${coreutils}/bin/base64'
    MKFIFO='${coreutils}/bin/mkfifo'
    MV='${coreutils}/bin/mv'
    PATHEGETTY='egetty'
    PATHGETTY='${utillinux}/bin/agetty'
    PATH='${coreutils}/bin:${gnused}/bin:${gnugrep}/bin:${gawk}/bin:/bin:/usr/bin:/etc'
    PATHVGETTY='vgetty'
    PCL6CMD='pcl6'
    PSPACKAGE='gs'
    RM='${coreutils}/bin/rm'
    SBIN='@out@/bin'
    SCRIPT_SH='/bin/sh'
    SED='${gnused}/bin/sed'
    SENDMAIL='/var/setuid-wrappers/sendmail'
    SPOOL='@out@/var/spool/hylafax'
    SYSVINIT='''
    TARGET='${_arch}-unknown-${_os}-gnu'
    TIFF2PDF='${libtiff}/bin/tiff2pdf'
    TIFFBIN='${libtiff}/bin'
    TTYCMD='${coreutils}/bin/tty'
    UUCP_LOCKDIR='@out@/var/lock/uucp'
    UUCP_LOCKTYPE='ascii'
    UUENCODE='${sharutils}/bin/uuencode'
  '';

  setup_modem = writeText "setup.modem" ''
    # Warning, setup.modem was automatically generated by NixOS
    prompt()
    {
       ${coreutils}/bin/echo -n "$* "
    }
          ttyPort()
          {
        ${coreutils}/bin/expr $1 : 'tty\(.*\)'
          }
          ttyLocks()
          {
        ${coreutils}/bin/echo $UUCP_LOCKDIR/LCK..`${coreutils}/bin/expr /$1 : '.*/\(.*\)'`
          }
          ttyAliases()
          {
        if [ -z "`${coreutils}/bin/echo $1 | ${gnugrep}/bin/grep "^/"`" ]; then ${coreutils}/bin/printf "/dev/"; fi
        ${coreutils}/bin/echo $1
          }
          ttyDev()
          {
        if [ -z "`${coreutils}/bin/echo $1 | ${gnugrep}/bin/grep "^/"`" ]; then ${coreutils}/bin/printf "/dev/"; fi
        ${coreutils}/bin/echo $1
          }
          checkPort()
          {
         return
          }
          ttyStty()
          {
        ${coreutils}/bin/echo ${coreutils}/bin/stty
          }
          ttySpeeds()
          {
        speeds=
        if [ -z "$SPEED" ]; then
            for s in 38400 19200 9600 4800 2400 1200; do
          onDev ${coreutils}/bin/stty $s </dev/null >/dev/null 2>&1 && speeds="$speeds $s"
            done
        fi
        ${coreutils}/bin/echo $speeds
          }
  '';

  config_site = writeText "config.site" ''
    DSO="auto" #dynamic shared objects
    GETTY="SysV" #witohut logwtmp
    HTML="no" #docs
    REGEX="no" #we use glibc's
    UTMP="auto"
    PS="gs" #no DPS\IMP in linux
    gs="yes"
    SGI2FAX="no" #SGI IRIX (gl/image.h)
    DPS="no" #SGI IRIX
    IMP="no" #SGI IRIX

    DIR_BIN="@out@/bin"
    DIR_SBIN="@out@/bin"
    DIR_LIBEXEC="@out@/bin"
    DIR_LIB="@out@/lib"
    DIR_LIBDATA="@out@/etc/hylafax"
    DIR_MAN="@out@/share/man"
    DIR_SPOOL="@out@/var/spool/hylafax"
    DIR_LOCKS="@out@/var/lock/uucp"
    FONTMAP="${ghostscript}/share/ghostscript/${ghostscript.version}"
    PATH_AFM="${ghostscript}/share/ghostscript/fonts"

    #unused. html docs.
    DIR_HTML="@out@/var/httpd/htdocs/hylafax"
    DIR_CGI="@out@/var/httpd/cgi-bin"
    HTMLPATH="/hylafax"
    CGIPATH="/cgi-bin"

    DEFVRES="196" #default fine quality
    FILLORDER="MSB2LSB"
    FAXGID="uucp"
    FAXUID="uucp"
    PAGESIZE="ISO A4"
    SYSGID="root"
    SYSUID="root"
    LOCKS="ascii"

    PATH_GETTY="${utillinux}/bin/agetty"
    PATH_VGETTY="vgetty"
    PATH_EGETTY="egetty"
    PATH_SENDMAIL="/var/setuid-wrappers/sendmail"
    PATH_GSRIP="${ghostscript}/bin/gs"
    PATH_DPSRIP="ps2fax.exe" #unused IRIX's DPS
    PATH_IMPRIP="psrip" #unused IRIX's IMP
    MANSCHEME="sysv-source-cat-strip"

    SYSVINIT="no" #we use systemd
    #reference for non-nixos linux systems.
    DIR_SYSVINIT="@out@/etc/rc.d/init.d" #unused
    DIR_SYSVINITSTART="../rc2.d ../rc3.d ../rc4.d ../rc5.d" #unused
    DIR_SYSVINITSTOP="../rc0.d ../rc1.d ../rc6.d" #unused
    NAME_SYSVINITSTART="S97hylafax" #unused
    NAME_SYSVINITSTOP="K05hylafax" #unused
    FAXQ_SERVER="yes"
    HFAXD_SERVER="yes"
    HFAXD_SNPP_SERVER="no" #pagers

    #svr4 pkginfo. unused?
    PKG_ARCH=${_arch}
    PKG_EMAIL=nix-dev@lists.science.uu.nl
    PKG_VENDOR="NixOS"

    AR="${binutils}/bin/ar"
    STRIP="${binutils}/bin/strip"

    LIBTIFF="-L${libtiff}/lib -ltiff"
    TIFFINC="-I${libtiff.dev}/include"
    TIFFBIN="${libtiff.bin}/bin"

    LIBZ="-L${zlib.out}/lib -lz"
    ZLIBINC="-I${zlib.dev}/include"

    #glibc's regex
    LIBREGEX=" "
    REGEXINC="-I${glibc.dev}/include"

    CONFIG_MAXGID="65534" #nobody on linux

    AWK="${gawk}/bin/awk"
    CAT="${coreutils}/bin/cat"
    CHGRP="${coreutils}/bin/chgrp"
    CHMOD="${coreutils}/bin/chmod"
    CHOWN="${coreutils}/bin/chown"
    CMP="${diffutils}/bin/cmp"
    COL="${utillinux}/bin/col"
    CP="${coreutils}/bin/cp"
    ECHO="${coreutils}/bin/echo"
    FILECMD="${file}/bin/file"
    FUSER="${psmisc}/bin/fuser"
    GREP="${gnugrep}/bin/grep"
    LN="${coreutils}/bin/ln"
    LN_S="-s"
    MAN="man"
    MIMENCODE="${coreutils}/bin/base64"
    MKDIR="${coreutils}/bin/mkdir"
    MKFIFO="${coreutils}/bin/mkfifo"
    MV="${coreutils}/bin/mv"
    MV_F="-f"
    PWDCMD="${coreutils}/bin/pwd"
    RMCMD="${coreutils}/bin/rm"
    SED="${gnused}/bin/sed"
    SCRIPT_SH="/bin/sh"
    SORT="${coreutils}/bin/sort"

    TIFF2PDF="${libtiff}/bin/tiff2pdf"
    TTYCMD="${coreutils}/bin/tty"
    UUENCODE="${sharutils}/bin/uuencode"
    MIMENCODE="mimencode" #needs metamail
    PCL6CMD="pcl6" #needs GhostPCL
  '';

  configurePhase = ''
    cp ${config_site} config.site
    substituteAllInPlace config.site
    ./configure --nointeractive
  '';

  hardeningDisable = [ "format" ];

  nativeBuildInputs = [ automake ];
  buildInputs = [
    ghostscript libtiff jbigkit libjpeg lcms2
    pam openldap
  ];

  postInstall = ''
    cp ${hyla_conf} $out/etc/hylafax/hyla.conf
    cp ${setup_cache} $out/var/spool/hylafax/etc/setup.cache
    cp ${setup_modem} $out/var/spool/hylafax/etc/setup.modem
    substituteAllInPlace $out/etc/hylafax/hyla.conf
    substituteAllInPlace $out/var/spool/hylafax/etc/setup.cache
    substituteAllInPlace $out/var/spool/hylafax/etc/setup.modem
    ln -s $out/var/spool/hylafax/bin/ps2fax.gs \
      $out/var/spool/hylafax/bin/ps2fax
    ln -s $out/var/spool/hylafax/bin/pdf2fax.gs \
      $out/var/spool/hylafax/bin/pdf2fax
    chmod +x $out/bin/* $out/var/spool/hylafax/bin/* \
      $out/etc/hylafax/faxmail/*/*
    #writeable spool
    mkdir -p $out/share/hylafax
    mv $out/var $out/share/hylafax
    ln -s /var $out/var
    #writeable etc
    mv $out/etc $out/share/hylafax
    ln -s /etc $out/etc
  '';

  meta = with stdenv.lib; {
    description = "Fax Management System";
    homepage = http://hylafax.sourceforge.net;
    license = licenses.bsd3;
    platforms = with platforms; linux;
    maintainers = with maintainers; [ ramkromberg ];
  };
}
