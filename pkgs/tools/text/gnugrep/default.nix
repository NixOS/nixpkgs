{
  lib,
  stdenv,
  updateAutotoolsGnuConfigScriptsHook,
  glibcLocales,
  fetchurl,
  pcre2,
  libiconv,
  perl,
  runtimeShellPackage,
}:

# Note: this package is used for bootstrapping fetchurl, and thus
# cannot use fetchpatch! All mutable patches (generated by GitHub or
# cgit) that are needed here should be included directly in Nixpkgs as
# files.

let
  version = "3.12";
in

stdenv.mkDerivation {
  pname = "gnugrep";
  inherit version;

  src = fetchurl {
    url = "mirror://gnu/grep/grep-${version}.tar.xz";
    hash = "sha256-JkmyfA6Q5jLq3NdXvgbG6aT0jZQd5R58D4P/dkCKB7k=";
  };

  patches = [
    # Fixes test-float-h failure on ppc64 with C23
    # https://lists.gnu.org/archive/html/bug-gnulib/2025-07/msg00021.html
    # Multiple upstream commits squashed with adjustments, see header
    ./gnulib-float-h-tests-port-to-C23-PowerPC-GCC.patch
  ]
  ++ lib.optionals stdenv.hostPlatform.isDarwin [
    # nl_langinfo is no longer thread safe on macOS 26.x+. (POSIX doesn't require it.)
    #
    # gnulib worked around the issue for macOS in:
    # https://github.com/coreutils/gnulib/commit/8ebd5a9aa7e24dce4ac2fd4f5074b43b00759d54
    #
    # We do not need to backport the gnulib fix for GNU grep because GNU grep
    # is not multi-threaded. See: https://debbugs.gnu.org/cgi/bugreport.cgi?bug=20768
    # Instead, we simply disable the relevant test that fails due to the issue.
    #
    # We can re-enable the test case once GNU grep updates their gnulib submodule to
    # include the above commit.
    ./darwin-disable-test-nl_langinfo-mt.patch
  ];

  # Some gnulib tests fail
  # - on Musl: https://github.com/NixOS/nixpkgs/pull/228714
  # - on x86_64-darwin: https://github.com/NixOS/nixpkgs/pull/228714#issuecomment-1576826330
  # - when building on Darwin (cross-compilation): test-nl_langinfo-mt fails
  postPatch =
    if stdenv.hostPlatform.isMusl || stdenv.buildPlatform.isDarwin then
      ''
        sed -i 's:gnulib-tests::g' Makefile.in
      ''
    else
      null;

  nativeCheckInputs = [
    perl
    glibcLocales
  ];
  outputs = [
    "out"
    "info"
  ]; # the man pages are rather small

  nativeBuildInputs = [ updateAutotoolsGnuConfigScriptsHook ];
  buildInputs = [
    pcre2
    libiconv
  ]
  ++ lib.optional (!stdenv.hostPlatform.isWindows) runtimeShellPackage;

  # cygwin: FAIL: multibyte-white-space
  # freebsd: FAIL mb-non-UTF8-performance
  # x86_64-darwin: fails 'stack-overflow' tests on Rosetta 2 emulator
  # aarch32: fails 'stack-overflow' when run on qemu under x86_64
  doCheck =
    !stdenv.hostPlatform.isCygwin
    && !stdenv.hostPlatform.isFreeBSD
    && !(stdenv.hostPlatform.isDarwin && stdenv.hostPlatform.isx86_64)
    && !stdenv.buildPlatform.isRiscV64
    && !stdenv.hostPlatform.isAarch32;

  # On macOS, force use of mkdir -p, since Grep's fallback
  # (./install-sh) is broken.
  preConfigure = ''
    export MKDIR_P="mkdir -p"
  '';

  configureFlags =
    # Work around build failure caused by the gnulib workaround for
    # https://gcc.gnu.org/bugzilla/show_bug.cgi?id=114870. remove after GCC 15
    lib.optional stdenv.hostPlatform.isCygwin "gl_cv_clean_version_stddef=yes";

  enableParallelBuilding = true;

  # Fix reference to sh in bootstrap-tools, and invoke grep via
  # absolute path rather than looking at argv[0].
  postInstall = ''
    rm $out/bin/egrep $out/bin/fgrep
    echo "#! /bin/sh" > $out/bin/egrep
    echo "exec $out/bin/grep -E \"\$@\"" >> $out/bin/egrep
    echo "#! /bin/sh" > $out/bin/fgrep
    echo "exec $out/bin/grep -F \"\$@\"" >> $out/bin/fgrep
    chmod +x $out/bin/egrep $out/bin/fgrep
  '';

  env = lib.optionalAttrs (stdenv.hostPlatform.isMinGW || stdenv.hostPlatform.isCygwin) {
    NIX_CFLAGS_COMPILE = "-Wno-error=format-security";
  };

  meta = {
    homepage = "https://www.gnu.org/software/grep/";
    description = "GNU implementation of the Unix grep command";

    longDescription = ''
      The grep command searches one or more input files for lines
      containing a match to a specified pattern.  By default, grep
      prints the matching lines.
    '';

    license = lib.licenses.gpl3Plus;

    maintainers = [
      lib.maintainers.das_j
      lib.maintainers.m00wl
    ];
    platforms = lib.platforms.all;
    mainProgram = "grep";
  };

  passthru = {
    inherit pcre2;
  };
}
