# afaik the longest dependency chain is stdenv -> stdenv-2#coreutils -> stdenv-2#gmp -> stdenv-1#libcxx -> stdenv-0#libc
# attempts were made to make stdenv-1#libcxx -> stdenv1#libc and collapse the entire chain by one step but it failed.
# effort to shorten bootstrap time is maybe better spent propagating the bootstrap binaries down to further stdenvs until they are no longer needed to remove spurious recompiles
{ lib
, localSystem, crossSystem, config, overlays, crossOverlays ? []
}:

assert crossSystem == localSystem;
let inherit (localSystem) system;
  all-bootstrap-urls-table = {
    # These bootstrap hashes generated by compiling from nixpkgs 46c6e8a75f2329633858ca13a589698fcbf6231c on x86_64-linux
    x86_64-freebsd = {
      bash = {
        url = "https://f005.backblazeb2.com/file/nixbsd/aldkfyqiq0nspljgl5pr0pgvxq99qirk-x86_64-freebsd-bash";
        hash = "sha256-6keIiXGHkqtaNM1A+ysI3H4FUmbQcjY6RMq+ET9ImQE=";
      };
      mkdir = {
        url = "https://f005.backblazeb2.com/file/nixbsd/f00mn41pls87615px9qjzhxkrcyi2wxc-x86_64-freebsd-mkdir";
        hash = "sha256-rPNJ8R4EgyXsdsgxUKg05JbtoWrsd+3Txlhn2fiyan4=";
      };
      tar = {
        url = "https://f005.backblazeb2.com/file/nixbsd/0j1x2kvmb1j2fi9jsmlaxxpg22mj2l59-x86_64-freebsd-tar";
        hash = "sha256-qhc9sfH/IpB7XodEwF+sU7784gyBtZtvL+KUnzjboBA=";
      };
      unxz = {
        url = "https://f005.backblazeb2.com/file/nixbsd/y14fgwj3080h9bqdv5llsfywnq9gj2dx-x86_64-freebsd-unxz";
        hash = "sha256-it5W1qmVMa6fChgf6m6gWgJSoqxLA3pcx8dus3BDhSE=";
      };
      chmod = {
        url = "https://f005.backblazeb2.com/file/nixbsd/iab27r5x6jaklqg11s4rib7kb4fi1hkf-x86_64-freebsd-chmod";
        hash = "sha256-iBdzCXKU+03MfOA75ASrXOD3arPtQnvKqsdCf32oKxk=";
      };
      bootstrapFiles = {
        url = "https://f005.backblazeb2.com/file/nixbsd/wvbr4if59gqhc537ai7g98p4s0nxds85-x86_64-freebsd-bootstrap-files.tar.xz";
        hash = "sha256-vYp8sGHg/Mu5nzQpZI2j9MInDydSQhaJYt8kMuSiLoM=";
        name = "bootstrap-files.tar.xz";
        executable = false;
      };
    };
  };
  fetchurlBoot = import <nix/fetchurl.nix>;
  all-bootstrap-urls = all-bootstrap-urls-table.${localSystem.system};
  all-bootstrap-files = lib.mapAttrs (a: v: fetchurlBoot (v // {
    executable = v.executable or true;
    name = v.name or a;
  })) all-bootstrap-urls;
  mkExtraBuildCommands0 = cc: ''
    rsrc="$out/resource-root"
    mkdir "$rsrc"
    ln -s "${cc.lib}/lib/clang/16/include" "$rsrc"
    echo "-resource-dir=$rsrc" >> $out/nix-support/cc-cflags
  '';
  mkExtraBuildCommands = cc: compiler-rt: mkExtraBuildCommands0 cc + ''
    ln -s "${compiler-rt.out}/lib" "$rsrc/lib"
    ln -s "${compiler-rt.out}/share" "$rsrc/share"
  '';
in
[
  ({}: let
    bootstrap-tools = (derivation {
      inherit system;
      name = "bootstrap-tools";
      pname = "bootstrap-tools";
      version = "9.9.9";
      builder = all-bootstrap-files.bash;
      args = [ ./unpack-bootstrap-files.sh ];
      inherit (all-bootstrap-files) tar unxz mkdir chmod bootstrapFiles;
    });

    linkBS = (attrs: derivation (attrs // {
      inherit system;
      name = attrs.name or (builtins.baseNameOf (builtins.elemAt attrs.paths 0));
      src = bootstrap-tools;
      builder = all-bootstrap-files.bash;
      args = [./linkBS.sh];
      paths = attrs.paths;
      PATH = "${bootstrap-tools}/bin";
    }));

    bootstrapLibs = linkBS { name = "bootstrapLibs"; paths = ["lib" "include" "share" "libexec"]; pname = "libs"; version = "bootstrap"; };
    bsdcp = linkBS { paths = [ "bin/bsdcp" ]; };
    patchelf = linkBS { paths = [ "bin/patchelf" ]; };
    bash = linkBS { paths = ["bin/bash" "bin/sh"]; shell = "bin/bash"; };
    curl = linkBS { paths = ["bin/curl" "bin/curl-config" ]; };
    clang = linkBS { paths = [ "bin/clang" "bin/clang++" "bin/cpp" ]; version = "16"; };
    coreutils = linkBS { name = "coreutils"; paths = map (str: "bin/" + str) [
      "base64" "basename" "cat" "chcon"
      "chgrp" "chmod" "chown" "chroot" "cksum"
      "comm" "cp" "csplit" "cut" "date"
      "dd" "df" "dir" "dircolors" "dirname"
      "du" "echo" "env" "expand" "expr"
      "factor" "false" "fmt" "fold" "groups"
      "head" "hostid" "id" "install"
      "join" "kill" "link" "ln" "logname"
      "ls" "md5sum" "mkdir" "mkfifo" "mknod"
      "mktemp" "mv" "nice" "nl" "nohup"
      "nproc" "numfmt" "od" "paste" "pathchk"
      "pinky" "pr" "printenv" "printf" "ptx"
      "pwd" "readlink" "realpath" "rm" "rmdir"
      "runcon" "seq" "shred" "shuf" "sleep"
      "sort" "split" "stat" "stdbuf" "stty"
      "sum" "tac" "tail" "tee" "test"
      "timeout" "touch" "tr" "true" "truncate"
      "tsort" "tty" "uname" "unexpand" "uniq"
      "unlink" "users" "vdir" "wc"
      "who" "whoami" "yes" "["
    ]; };
    diffutils = linkBS { name = "diffutils"; paths = map (str: "bin/" + str) [
      "diff" "cmp" "diff3" "sdiff"
    ]; };
    findutils = linkBS { name = "findutils"; paths = [ "bin/find" "bin/xargs" ]; };
    iconv = linkBS { paths = [ "bin/iconv" ]; };
    patch = linkBS { paths = [ "bin/patch" ]; };
    gnutar = linkBS { paths = [ "bin/tar" ]; };
    gawk = linkBS { paths = [ "bin/awk" "bin/gawk" ]; };
    gnumake = linkBS { paths = [ "bin/make" ]; };
    gnugrep = linkBS { paths = [ "bin/grep" "bin/egrep" "bin/fgrep" ]; };
    gnused = linkBS { paths = [ "bin/sed" ]; };
    gzip = linkBS { paths = [ "bin/gzip" "bin/gunzip" ]; };
    bzip2 = linkBS { paths = [ "bin/bzip2" ]; };
    xz = linkBS { paths = [ "bin/xz" "bin/unxz" ]; };
    cacert = linkBS { paths = [ "etc/ssl/certs" "nix-support/setup-hook" ]; };
    binutils = linkBS { name = "binutils"; paths = map (str: "bin/" + str) [
      "ld" "as" "addr2line" "ar" "c++filt"
      "elfedit" "gprof" "objdump"
      "nm" "objcopy" "ranlib" "readelf" "size"
      "strings" "strip"
    ]; };
    locales = linkBS { paths = ["share/locale"]; };
    libcxx = linkBS {
      name = "libcxx";
      paths = ["lib/libc++.so" "lib/libc++.so.1"];
    };


    fetchurl = import ../../build-support/fetchurl {
      inherit lib curl;
      stdenvNoCC = stdenvNoCC;
    };

    stdenvNoCC = import ../generic {
      inherit config;
      name = "freebsd-boot-0-stdenvNoCC";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [coreutils gnutar findutils gnumake gnused patchelf gnugrep gawk diffutils patch bash xz gzip bzip2 bsdcp];
      shell = "${bash}/bin/bash";
      fetchurlBoot = fetchurl;
      cc = null;
    };

    stdenv = import ../generic {
      inherit config;
      name = "stdenv-freebsd-boot-0";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [coreutils gnutar findutils gnumake gnused patchelf gnugrep gawk diffutils patch bash xz gzip bzip2 bsdcp];
      shell = "${bash}/bin/bash";
      fetchurlBoot = fetchurl;
      extraNativeBuildInputs = [./unpack-source.sh ./always-patchelf.sh ./autotools-abspath.sh];
      cc = import ../../build-support/cc-wrapper ({
        inherit lib;
        name = "freebsd-boot-0-cc";
        nixSupport = {
          libcxx-cxxflags = ["-isystem ${bootstrapLibs}/include/c++/v1"];
        };
        stdenvNoCC = stdenvNoCC;
        libc = bootstrapLibs;
        propagateDoc = false;
        nativeTools = false;
        nativeLibc = false;
        gnugrep = gnugrep;
        coreutils = coreutils;
        cc = clang;
        isClang = true;
        bintools = import ../../build-support/bintools-wrapper {
          inherit lib;
          stdenvNoCC = stdenvNoCC;
          name = "freebsd-boot-0-bintools";
          libc = bootstrapLibs;
          propagateDoc = false;
          nativeTools = false;
          nativeLibc = false;
          bintools = binutils;
          gnugrep = gnugrep;
          coreutils = coreutils;
        };
      });
      overrides = self: super: {
        gnutar = super.gnutar.overrideAttrs {
          NIX_LDFLAGS = "-lintl";  # picks up libintl.h but not libintl.so from bootstrap
        };
        gettext = super.gettext.overrideAttrs {
          NIX_CFLAGS_COMPILE = "-DHAVE_ICONV=1";  # we clearly have iconv. what do you want?
        };
        inherit fetchurl curl cacert iconv;
        curlReal = super.curl;
        freebsd = super.freebsd.overrideScope (self': super': {
          inherit locales;
        });
      };
      preHook = ''
          export NIX_ENFORCE_PURITY="''${NIX_ENFORCE_PURITY-1}"
          export NIX_ENFORCE_NO_NATIVE="''${NIX_ENFORCE_NO_NATIVE-1}"
        '';
    };
  in { inherit config overlays stdenv; })

  (prevStage: let
    fetchurlBoot = import ../../build-support/fetchurl {
      inherit lib stdenvNoCC;
      curl = prevStage.curlReal;
    };

    bsdcp = (prevStage.runCommand "bsdcp" {} "mkdir -p $out/bin; cp ${prevStage.freebsd.cp}/bin/cp $out/bin/bsdcp");

    stdenvNoCC = import ../generic {
      inherit config fetchurlBoot;
      name = "stdenvNoCC-freebsd-boot-1";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [ prevStage.coreutils prevStage.gnutar prevStage.findutils prevStage.gnumake prevStage.gnused prevStage.patchelf prevStage.gnugrep prevStage.gawk prevStage.diffutils prevStage.patch prevStage.bash prevStage.gzip prevStage.bzip2 prevStage.xz bsdcp];
      shell = "${prevStage.bash}/bin/bash";
      cc = null;
    };

    in rec {
    inherit config overlays;
    stdenv = import ../generic {
      inherit config fetchurlBoot;
      name = "stdenv-freebsd-boot-1";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [ prevStage.coreutils prevStage.gnutar prevStage.findutils prevStage.gnumake prevStage.gnused prevStage.patchelf prevStage.gnugrep prevStage.gawk prevStage.diffutils prevStage.patch prevStage.bash prevStage.gzip prevStage.bzip2 prevStage.xz bsdcp];
      extraNativeBuildInputs = [./unpack-source.sh ./always-patchelf.sh ./autotools-abspath.sh];
      shell = "${prevStage.bash}/bin/bash";
      cc = import ../../build-support/cc-wrapper ({
        inherit lib stdenvNoCC;
        name = "freebsd-boot-1-cc";
        inherit (prevStage.freebsd) libc;
        inherit (prevStage) gnugrep coreutils libcxx;
        propagateDoc = false;
        nativeTools = false;
        nativeLibc = false;
        cc = prevStage.llvmPackages.clang-unwrapped;
        isClang = true;
        extraPackages = [
          prevStage.llvmPackages.compiler-rt
          prevStage.llvmPackages.libunwind
        ];
        nixSupport = {
          libcxx-ldflags = ["-lunwind"];
        };
        extraBuildCommands = mkExtraBuildCommands prevStage.llvmPackages.clang-unwrapped prevStage.llvmPackages.compiler-rt;
        bintools = import ../../build-support/bintools-wrapper {
          inherit lib stdenvNoCC;
          name = "freebsd-boot-1-bintools";
          inherit (prevStage.freebsd) libc;
          inherit (prevStage) gnugrep coreutils;
          bintools = prevStage.binutils-unwrapped;
          propagateDoc = false;
          nativeTools = false;
          nativeLibc = false;
        };
      });
      overrides = self: super: {
        curl = prevStage.curlReal;
        curlReal = super.curl;
        bash = prevStage.bash;
        bashReal = super.bash;
        gitMinimal = prevStage.gitMinimal;
        freebsd = super.freebsd.overrideScope (self': super': {
          #stdenv = prevStage.overrideCC stdenv prevStage.llvmPackages_16.clang;
          locales = prevStage.freebsd.locales;
        });
      };
      preHook = ''
          export NIX_ENFORCE_PURITY="''${NIX_ENFORCE_PURITY-1}"
          export NIX_ENFORCE_NO_NATIVE="''${NIX_ENFORCE_NO_NATIVE-1}"
        '';
    };
  })

  (prevStage: let
    fetchurlBoot = import ../../build-support/fetchurl {
      inherit lib stdenvNoCC;
      curl = prevStage.curlReal;
    };

    bsdcp = (prevStage.runCommand "bsdcp" {} "mkdir -p $out/bin; cp ${prevStage.freebsd.cp}/bin/cp $out/bin/bsdcp");

    stdenvNoCC = import ../generic {
      inherit config fetchurlBoot;
      name = "stdenvNoCC-freebsd-boot-2";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [ prevStage.coreutils prevStage.gnutar prevStage.findutils prevStage.gnumake prevStage.gnused prevStage.patchelf prevStage.gnugrep prevStage.gawk prevStage.diffutils prevStage.patch prevStage.bashReal prevStage.gzip prevStage.bzip2 prevStage.xz bsdcp];
      shell = "${prevStage.bashReal}/bin/bash";
      cc = null;
    };

    in rec {
    inherit config overlays;
    stdenv = import ../generic {
      inherit config fetchurlBoot;
      name = "stdenv-freebsd-boot-2";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [ prevStage.coreutils prevStage.gnutar prevStage.findutils prevStage.gnumake prevStage.gnused prevStage.patchelf prevStage.gnugrep prevStage.gawk prevStage.diffutils prevStage.patch prevStage.bashReal prevStage.gzip prevStage.bzip2 prevStage.xz bsdcp];
      extraNativeBuildInputs = [./unpack-source.sh ./always-patchelf.sh ./autotools-abspath.sh];
      shell = "${prevStage.bashReal}/bin/bash";
      cc = import ../../build-support/cc-wrapper ({
        inherit lib stdenvNoCC;
        name = "freebsd-boot-2-cc";
        inherit (prevStage.freebsd) libc;
        inherit (prevStage) gnugrep coreutils;
        inherit (prevStage.llvmPackages) libcxx;
        propagateDoc = false;
        nativeTools = false;
        nativeLibc = false;
        cc = prevStage.llvmPackages.clang-unwrapped;
        isClang = true;
        extraPackages = [
          prevStage.llvmPackages.compiler-rt
          prevStage.llvmPackages.libunwind
        ];
        nixSupport = {
          libcxx-ldflags = ["-lunwind"];
        };
        extraBuildCommands = mkExtraBuildCommands prevStage.llvmPackages.clang-unwrapped prevStage.llvmPackages.compiler-rt;
        bintools = import ../../build-support/bintools-wrapper {
          inherit lib stdenvNoCC;
          name = "freebsd-boot-2-bintools";
          inherit (prevStage.freebsd) libc;
          inherit (prevStage) gnugrep coreutils;
          bintools = prevStage.binutils-unwrapped;
          propagateDoc = false;
          nativeTools = false;
          nativeLibc = false;
          buildPackages = prevStage.buildPackages;
        };
      });
      overrides = self: super: {
        curl = prevStage.curlReal;
        curlReal = super.curl;
        cacert = prevStage.cacert;
        inherit (prevStage) fetchurl;
        freebsd = super.freebsd.overrideScope (self': super': {
          locales = prevStage.freebsd.locales;
          localesReal = super'.locales;
        });
        haskellPackages = super.haskellPackages.override {
          overrides = (self: super: {
            digest = super.digest.overrideAttrs {
              postPatch = ''
                sed -E -i -e 's/ && !os\(freebsd\)//' digest.cabal
              '';
            };
          });
        };
      };
      preHook = ''
          export NIX_ENFORCE_PURITY="''${NIX_ENFORCE_PURITY-1}"
          export NIX_ENFORCE_NO_NATIVE="''${NIX_ENFORCE_NO_NATIVE-1}"
        '';
    };
  })

  (prevStage: let
    fetchurlBoot = import ../../build-support/fetchurl {
      inherit lib stdenvNoCC;
      curl = prevStage.curlReal;
    };

    bsdcp = (prevStage.runCommand "bsdcp" {} "mkdir -p $out/bin; cp ${prevStage.freebsd.cp}/bin/cp $out/bin/bsdcp");

    stdenvNoCC = import ../generic {
      inherit config fetchurlBoot;
      name = "stdenvNoCC-freebsd";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [ prevStage.coreutils prevStage.gnutar prevStage.findutils prevStage.gnumake prevStage.gnused prevStage.patchelf prevStage.gnugrep prevStage.gawk prevStage.diffutils prevStage.patch prevStage.bash prevStage.gzip prevStage.bzip2 prevStage.xz bsdcp];
      shell = "${prevStage.bash}/bin/bash";
      cc = null;
    };

    in rec {
    inherit config overlays;
    stdenv = import ../generic {
      inherit config fetchurlBoot;
      name = "stdenv-freebsd";
      buildPlatform = localSystem;
      hostPlatform = localSystem;
      targetPlatform = localSystem;
      initialPath = [ prevStage.coreutils prevStage.gnutar prevStage.findutils prevStage.gnumake prevStage.gnused prevStage.patchelf prevStage.gnugrep prevStage.gawk prevStage.diffutils prevStage.patch prevStage.bash prevStage.gzip prevStage.bzip2 prevStage.xz bsdcp];
      extraNativeBuildInputs = [./unpack-source.sh ./always-patchelf.sh ./autotools-abspath.sh];
      shell = "${prevStage.bash}/bin/bash";
      cc = lib.makeOverridable (import ../../build-support/cc-wrapper) {
        inherit lib stdenvNoCC;
        name = "freebsd-cc";
        inherit (prevStage.freebsd) libc;
        inherit (prevStage) gnugrep coreutils;
        inherit (prevStage.llvmPackages) libcxx;
        propagateDoc = false;
        nativeTools = false;
        nativeLibc = false;
        cc = prevStage.llvmPackages.clang-unwrapped;
        isClang = true;
        extraPackages = [
          prevStage.llvmPackages.compiler-rt
          prevStage.llvmPackages.libunwind
        ];
        nixSupport = {
          libcxx-ldflags = ["-lunwind"];
        };
        extraBuildCommands = mkExtraBuildCommands prevStage.llvmPackages.clang-unwrapped prevStage.llvmPackages.compiler-rt;
        bintools = import ../../build-support/bintools-wrapper {
          inherit lib stdenvNoCC;
          name = "freebsd-bintools";
          inherit (prevStage.freebsd) libc;
          inherit (prevStage) gnugrep coreutils;
          bintools = prevStage.binutils-unwrapped;
          propagateDoc = false;
          nativeTools = false;
          nativeLibc = false;
          buildPackages = prevStage.buildPackages;
        };
      };
      overrides = self: super: {
        curl = prevStage.curlReal;
        cacert = prevStage.cacert;
        inherit (prevStage) fetchurl;
        freebsd = super.freebsd.overrideScope (self': super': {
          localesPrev = prevStage.freebsd.localesReal;
        });
        haskellPackages = super.haskellPackages.override {
          overrides = (self: super: {
            digest = super.digest.overrideAttrs {
              postPatch = ''
                sed -E -i -e 's/ && !os\(freebsd\)//' digest.cabal
              '';
            };
          });
        };
      };
      preHook = ''
          export NIX_ENFORCE_PURITY="''${NIX_ENFORCE_PURITY-1}"
          export NIX_ENFORCE_NO_NATIVE="''${NIX_ENFORCE_NO_NATIVE-1}"
          export PATH_LOCALE=${prevStage.freebsd.localesReal}/share/locale
        '';
    };
  })
]
