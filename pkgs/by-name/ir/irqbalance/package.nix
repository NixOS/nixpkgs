{
  lib,
  stdenv,
  fetchFromGitHub,
  autoreconfHook,
  pkg-config,
  glib,
  ncurses,
  libcap_ng,
  enableSystemd ? lib.meta.availableOn stdenv.hostPlatform systemdMinimal,
  systemdMinimal,
}:

stdenv.mkDerivation rec {
  pname = "irqbalance";
  version = "1.9.4";

  src = fetchFromGitHub {
    owner = "irqbalance";
    repo = "irqbalance";
    rev = "v${version}";
    sha256 = "sha256-7es7wwsPnDSF37uL5SCgAQB+u+qGWmWDHOh3JkHuXMs=";
  };

  nativeBuildInputs = [
    autoreconfHook
    pkg-config
  ];
  buildInputs = [
    glib
    ncurses
    libcap_ng
  ]
  ++ (lib.optionals enableSystemd [
    systemdMinimal
  ]);

  configureFlags = lib.optionals enableSystemd [
    "--with-systemd"
  ];

  postInstall = ''
    # Systemd service
    mkdir -p "$out/lib/systemd/system"
    grep -vi "EnvironmentFile" misc/irqbalance.service >"$out/lib/systemd/system/irqbalance.service"
    substituteInPlace "$out/lib/systemd/system/irqbalance.service" \
      --replace-fail /usr/sbin/irqbalance "$out/bin/irqbalance --journal" \
      --replace-fail ' $IRQBALANCE_ARGS' ""
  '';

  meta = {
    homepage = "https://github.com/Irqbalance/irqbalance";
    changelog = "https://github.com/Irqbalance/irqbalance/releases/tag/v${version}";
    description = "Daemon to help balance the cpu load generated by interrupts across all of a systems cpus";
    license = lib.licenses.gpl2Only;
    platforms = lib.platforms.linux;
    maintainers = with lib.maintainers; [ moni ];
  };
}
