{
  lib,
  rustPlatform,
  fetchFromGitHub,
  nix-update-script,
  pkg-config,
  cmake,
  git,
  alsa-lib,
  libclang,
  makeWrapper,
  dotool,
  wtype,
  wl-clipboard,
  ydotool,
}:

rustPlatform.buildRustPackage (finalAttrs: {
  pname = "voxtype";
  version = "0.4.16";

  src = fetchFromGitHub {
    owner = "peteonrails";
    repo = "voxtype";
    tag = "v${finalAttrs.version}";
    hash = "sha256-4sIiZi/VDMamGMRZZSrMcOwTk8mOvKkwQGtRYUhKfLE=";
  };

  cargoHash = "sha256-M7XTk/f20m3OHzns3Pnk/I7wWH6GUUCzgVLHvH9xznw=";

  nativeBuildInputs = [
    pkg-config
    cmake
    git
    makeWrapper
  ];

  buildInputs = [
    alsa-lib
  ];

  env = {
    LIBCLANG_PATH = "${libclang.lib}/lib";
    # Trigger man page generation in build.rs
    VOXTYPE_GEN_MANPAGES = "1";
  };

  postInstall = ''
    wrapProgram $out/bin/voxtype \
      --prefix PATH : ${
        lib.makeBinPath [
          dotool
          wtype
          wl-clipboard
          ydotool
        ]
      }

    # Install man pages generated by build.rs
    # Man pages are generated in target/<triple>/release/build/voxtype-*/out/man
    mandir=$(find target -path '*/build/voxtype-*/out/man' -type d | head -1)
    if [ -n "$mandir" ] && [ -d "$mandir" ]; then
      install -Dm644 "$mandir"/*.1 -t $out/share/man/man1/
    fi
  '';

  passthru.updateScript = nix-update-script { };

  meta = {
    description = "Open-source push-to-talk voice-to-text application for Linux desktops";
    longDescription = ''
      Voxtype is a push-to-talk voice-to-text application specifically designed
      for Linux desktops. It uses whisper.cpp for offline speech recognition
      and supports both Wayland and X11 desktops including GNOME, KDE, Sway,
      Hyprland, and River.
    '';
    homepage = "https://github.com/peteonrails/voxtype";
    changelog = "https://github.com/peteonrails/voxtype/releases/tag/v${finalAttrs.version}";
    license = lib.licenses.mit;
    maintainers = with lib.maintainers; [ vdemeester ];
    platforms = lib.platforms.linux;
    mainProgram = "voxtype";
  };
})
