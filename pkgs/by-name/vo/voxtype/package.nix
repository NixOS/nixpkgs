{
  lib,
  rustPlatform,
  fetchFromGitHub,
  nix-update-script,
  pkg-config,
  cmake,
  git,
  alsa-lib,
  libclang,
  makeWrapper,
  wtype,
  wl-clipboard,
  ydotool,
}:

rustPlatform.buildRustPackage (finalAttrs: {
  pname = "voxtype";
  version = "0.4.9";

  src = fetchFromGitHub {
    owner = "peteonrails";
    repo = "voxtype";
    tag = "v${finalAttrs.version}";
    hash = "sha256-DSxn41dNlxyNot7btnx2yErp0Ehj3zUZ9F1e+mhZvM0=";
  };

  cargoHash = "sha256-/KShjpQe3d5lbE9UzkGIFmf31Xb3vdxiFKRS8wtWf1M=";

  nativeBuildInputs = [
    pkg-config
    cmake
    git
    makeWrapper
  ];

  buildInputs = [
    alsa-lib
  ];

  env = {
    LIBCLANG_PATH = "${libclang.lib}/lib";
    # Trigger man page generation in build.rs
    VOXTYPE_GEN_MANPAGES = "1";
  };

  postInstall = ''
    wrapProgram $out/bin/voxtype \
      --prefix PATH : ${
        lib.makeBinPath [
          wtype
          wl-clipboard
          ydotool
        ]
      }

    # Install man pages generated by build.rs
    # Man pages are generated in target/<triple>/release/build/voxtype-*/out/man
    mandir=$(find target -path '*/build/voxtype-*/out/man' -type d | head -1)
    if [ -n "$mandir" ] && [ -d "$mandir" ]; then
      install -Dm644 "$mandir"/*.1 -t $out/share/man/man1/
    fi
  '';

  passthru.updateScript = nix-update-script { };

  meta = {
    description = "Open-source push-to-talk voice-to-text application for Linux desktops";
    longDescription = ''
      Voxtype is a push-to-talk voice-to-text application specifically designed
      for Linux desktops. It uses whisper.cpp for offline speech recognition
      and supports both Wayland and X11 desktops including GNOME, KDE, Sway,
      Hyprland, and River.
    '';
    homepage = "https://github.com/peteonrails/voxtype";
    changelog = "https://github.com/peteonrails/voxtype/releases/tag/v${finalAttrs.version}";
    license = lib.licenses.mit;
    maintainers = with lib.maintainers; [ vdemeester ];
    platforms = lib.platforms.linux;
    mainProgram = "voxtype";
  };
})
