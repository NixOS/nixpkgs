{ lib
, buildNpmPackage
, importNpmLock
, fetchFromGitHub
}:

buildNpmPackage rec {
  pname = "live-server";
  version = "1.2.1";

  src = fetchFromGitHub {
    owner = "tapio";
    repo = "live-server";
    rev = version;
    hash = "sha256-x24QCQU/QnLugEsti6lgIQvO2O+kxypt582qSDTfPOw=";
  };

  # Upstream doesn't git track a package-lock.json file, so we downloaded
  # manually both package.json and the package-lock.json file generated by
  # running npm install at the repository root. See also upstream report:
  # https://github.com/tapio/live-server/issues/430
  #
  # Because upstream seems unresponsive, these files won't require frequent
  # updates.
  npmDeps = importNpmLock {
    npmRoot = ./.;
  };
  npmConfigHook = importNpmLock.npmConfigHook;

  # Pure nodejs package, no need to build or prune anything
  dontNpmBuild = true;
  dontNpmPrune = true;

  meta = with lib; {
    description = "A simple development http server with live reload capability";
    homepage = "https://github.com/tapio/live-server";
    license = licenses.mit;
    maintainers = with maintainers; [ doronbehar ];
    mainProgram = "live-server";
    platforms = platforms.all;
  };
}
