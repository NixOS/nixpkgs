{
  lib,
  stdenvNoCC,
  fetchFromGitHub,
  bc,
  bloaty,
  cached-nix-shell,
  coreutils,
  curl,
  gawk,
  gh,
  gnused,
  hydra-check,
  mdcat,
  nix,
  nixpkgs-hammering,
  nixpkgs-review,
  jq,
  pup,
  python3Packages,
  ripgrep,
  savepagenow,
  makeWrapper,
  unstableGitUpdater,
}:

stdenvNoCC.mkDerivation (finalAttrs: {
  name = "nixpkgs-review-checks";
  version = "0-unstable-2024-07-01";

  src = fetchFromGitHub {
    owner = "SuperSandro2000";
    repo = "nixpkgs-review-checks";
    rev = "f1b9ae8518b3b82b1c9799c78a128e829faa1e4f";
    hash = "sha256-ReLuuyItfOXsnWiC5UocOUpKZoiL9xaV4LXp0THM3es=";
  };

  nativeBuildInputs = [ makeWrapper ];

  buildInputs = [
    bc
    bloaty
    cached-nix-shell
    coreutils
    curl
    gawk
    gh
    gnused
    hydra-check
    mdcat
    nix
    nixpkgs-hammering
    nixpkgs-review
    jq
    pup
    python3Packages.ansi2html
    ripgrep
    savepagenow
  ];

  installPhase = ''
    install -Dm755 nixpkgs-review-checks -t $out/bin
    install -Dm755 nixpkgs-review-checks-hook -t $out/etc/profile.d
    install -Dm644 ofborg.graphql -t $out/share/nixpkgs-review-checks
    wrapProgram $out/bin/nixpkgs-review-checks \
      --prefix PATH : ${lib.makeBinPath finalAttrs.buildInputs} \
      --set NIXPKGS_REVIEW_CHECKS_GRAPHQL_FILE $out/share/nixpkgs-review-checks/ofborg.graphql
  '';

  passthru.updateScript = unstableGitUpdater { };

  meta = {
    description = "Adds additional checks and information to reports generated by nixpkgs-review";
    homepage = "https://github.com/SuperSandro2000/nixpkgs-review-checks";
    license = lib.licenses.mit;
    maintainers = with lib.maintainers; [
      pandapip1
      SuperSandro2000
    ];
    platforms = lib.platforms.all;
  };
})
