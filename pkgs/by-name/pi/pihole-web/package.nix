{ stdenv, lib, fetchFromGitHub, php, pihole, pihole-ftl, procps }:

stdenv.mkDerivation (finalAttrs: {
  pname = "pihole-web";
  version = "5.21";

  src = fetchFromGitHub {
    owner = "pi-hole";
    repo = "web";
    rev = "v${finalAttrs.version}";
    hash = "sha256-+3Dl+SnV4XIMxk9+EZT+s1/43zJXq/QHYwjRSum8gdE=";
  };

  propagatedBuildInputs = [
    pihole
    pihole-ftl
    procps
  ];

  postPatch = ''
    # Redirect stderr to stdout so that errors appear on the Update Gravity page
    substituteInPlace scripts/pi-hole/php/gravity.sh.php \
      --replace-fail 'sudo pihole -g' '${pihole}/bin/pihole -g 2>&1'

    substituteInPlace settings.php \
      --replace-fail '/usr/bin/pihole-FTL' ${pihole-ftl}/bin/pihole-FTL

    substituteInPlace scripts/pi-hole/php/func.php \
      --replace-fail 'pidof pihole-FTL' '${procps}/bin/pidof pihole-FTL' \
      --replace-fail "ps -p" "${procps}/bin/ps -p" \
      --replace-fail 'sudo pihole' ${pihole}/bin/pihole
  '';

  installPhase = ''
    runHook preInstall

    mkdir -p $out/share
    cp -r -t $out/share *.php img/ scripts/ style/

    mkdir -p $out/share/doc/$name/
    cp README.md $out/share/doc/$name/

    runHook postInstall
  '';

  meta = {
    description = "Pi-hole web dashboard displaying stats and more";
    longDescription = ''
      Pi-hole's Web interface (based off of AdminLTE) provides a central
      location to manage your Pi-hole and review the statistics generated by
      FTLDNS.
    '';
    license = lib.licenses.eupl12;
    maintainers = with lib.maintainers; [ williamvds ];
    platforms = lib.platforms.linux;
  };
})
