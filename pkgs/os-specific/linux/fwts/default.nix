{ lib, stdenv, fetchzip, autoreconfHook, pkg-config, glib, libtool, pcre
, json_c, flex, bison, dtc, pciutils, dmidecode, iasl, libbsd }:

stdenv.mkDerivation rec {
  pname = "fwts";
  version = "21.01.00";

  src = fetchzip {
    url = "http://fwts.ubuntu.com/release/${pname}-V${version}.tar.gz";
    sha256 = "sha256-3NNl/48GH4FMoAlc35+kr5rnfeMfOa9iq/f70g0boOE=";
    stripRoot = false;
  };

  nativeBuildInputs = [ autoreconfHook pkg-config libtool ];
  buildInputs = [ glib pcre json_c flex bison dtc pciutils dmidecode iasl libbsd ];

  postPatch = ''
    substituteInPlace src/lib/include/fwts_binpaths.h --replace "/usr/bin/lspci"      "${pciutils}/bin/lspci"
    substituteInPlace src/lib/include/fwts_binpaths.h --replace "/usr/sbin/dmidecode" "${dmidecode}/bin/dmidecode"
    substituteInPlace src/lib/include/fwts_binpaths.h --replace "/usr/bin/iasl"       "${iasl}/bin/iasl"
  '';

  enableParallelBuilding = true;

  meta = with lib; {
    homepage = "https://wiki.ubuntu.com/FirmwareTestSuite";
    description = "Firmware Test Suite";
    platforms = platforms.linux;
    license = licenses.gpl2;
    maintainers = with maintainers; [ tadfisher ];
  };
}
