{ stdenv, lib, fetchurl, automake, autoreconfHook, coreutils, dmidecode
, glibcLocales, hwdata, kmod, libtool, makeWrapper, sqlite, perl }:
stdenv.mkDerivation rec {
  pname = "rasdaemon";
  version = "0.6.4";
  src = fetchurl {
    url =
      "https://www.infradead.org/~mchehab/rasdaemon/rasdaemon-${version}.tar.bz2";
    sha256 = "0p107x5nl5fm6n46sv45fk596z63j33j13aw076rs09m6bsgfwm6";
  };

  nativeBuildInputs = [ automake libtool makeWrapper ];
  buildInputs = [
    coreutils
    dmidecode
    glibcLocales
    hwdata
    kmod
    sqlite
    (perl.withPackages (ps: with ps; [ DBI DBDSQLite ]))
  ];

  configureFlags = [
    "--sysconfdir=/etc"
    "--localstatedir=/var"
    # In Debian and Arch Linux
    "--enable-abrt-report"
    "--enable-aer"
    "--enable-arm"
    "--enable-extlog"
    "--enable-mce"
    "--enable-sqlite3"
    # In Arch Linux
    "--enable-devlink"
    "--enable-diskerror"
    "--enable-hisi-ns-decode"
    "--enable-non-standard"
  ];

  # The Makefile attempts to create the following directories:
  # /var/lib/rasdaemon
  #   location of the RAS event log generated by rasdaemon -r
  # /etc/ras/dimm_labels.d
  #   location of the DIMM labels generated by ras-mc-ctl
  # We can not create these directories at install time,
  # since the nix builder does not have permissions.
  # Since these programs need permissions to these directories
  # to run properly, we wrap the executables to create
  # these directories on startup. (see postFixup)
  postConfigure = ''
    sed -e '/$(install_sh) -d "$(DESTDIR)\/var\/lib\/rasdaemon"/d' \
    -e '/$(install_sh) -d "$(DESTDIR)\/etc\/ras\/dimm_labels.d"/d' \
    -i Makefile
  '';

  doCheck = true;

  installFlags = [
    "sysconfdir=${placeholder "out"}/etc"
    "localstatedir=${placeholder "out"}/var"
  ];

  postInstall = ''
    install -D -p -m 0644 misc/rasdaemon.service \
      $out/lib/systemd/system/rasdaemon.service
    install -D -p -m 0644 misc/ras-mc-ctl.service \
      $out/lib/systemd/system/ras-mc-ctl.service
  '';

  postFixup = ''
    # Fix dmidecode and modprobe paths
    substituteInPlace $out/sbin/ras-mc-ctl \
      --replace 'find_prog ("dmidecode")' '"${dmidecode}/bin/dmidecode"' \
      --replace 'find_prog ("modprobe")  or exit (1)' '"${kmod}/bin/modprobe"'
    # Create database and label dirs on startup
    wrapProgram $out/sbin/rasdaemon \
      --run "${coreutils}/bin/mkdir -p /var/lib/rasdaemon"
    wrapProgram $out/sbin/ras-mc-ctl \
      --run "${coreutils}/bin/mkdir -p /etc/ras/dimm_labels.d"
    # Patch in LOCALE_ARCHIVE into systemd service
    s='Environment="LOCALE_ARCHIVE=${glibcLocales}\/lib\/locale\/locale-archive"'
    sed -e '/\[Service\]/a '"$s" \
      -i $out/lib/systemd/system/ras-mc-ctl.service
  '';

  meta = with stdenv.lib; {
    description = ''
      A Reliability, Availability and Serviceability (RAS) logging tool using
      EDAC kernel tracing events
    '';
    longDescription = ''
      Rasdaemon is a RAS (Reliability, Availability and Serviceability) logging
      tool. It records memory errors, using the EDAC tracing events. EDAC is a
      Linux kernel subsystem with handles detection of ECC errors from memory
      controllers for most chipsets on i386 and x86_64 architectures. EDAC
      drivers for other architectures like arm also exists.
    '';
    homepage = "https://pagure.io/rasdaemon";
    license = licenses.gpl2;
    platforms = platforms.linux;
    changelog =
      "http://git.infradead.org/users/mchehab/rasdaemon.git/blob/refs/tags/v${version}:/ChangeLog";
    maintainers = with maintainers; [ leocp1 ];
  };
}
