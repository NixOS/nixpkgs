{
  lib,
  stdenv,
  kernel,
  openssl,
  pkg-config,
  writeShellScriptBin,
}:

let
  # taken from: https://docs.kernel.org/admin-guide/module-signing.html
  #
  # Slightly adapted: changed -sha256 to -sha512
  #
  # Attention: THIS IS AN EXAMPLE AND NOT PRODUCTION GRADE
  #     - You probably want to customize this and use e.g. a much lower
  #       lifetime!
  #     - You need to secure your private key or delete it immediately after signing!
  #     - You may want to store your private key(s) in an HSM or at least in tmpfs and
  #       not persist them unencrpyted.
  default-keygen = writeShellScriptBin "kernel-module-default-keygen" ''
    ${openssl}/bin/openssl req -new -nodes -utf8 -sha512 -days 36500 -batch -x509 \
      -config #CONFIG_PATH#/usr/share/kernel-module-signing/default_x509.genkey \
      -outform PEM -out kernel_cert.pem \
      -keyout kernel_key.pem
  '';
in
stdenv.mkDerivation {
  pname = "linux-kernel-module-signing-tools";
  inherit (kernel) version src;

  postPatch = ''
    pushd scripts
    export CFLAGS=$(pkg-config --cflags --libs libcrypto)
    substituteInPlace sign-file.c \
      --replace-fail scripts/sign-file sign-file
    popd
  '';

  buildPhase = ''
    pushd scripts
    cc -o sign-file -O2 `pkg-config --libs --cflags libcrypto` sign-file.c
    popd
    pushd certs
    cc -o extract-cert -O2 `pkg-config --libs --cflags libcrypto` -I../scripts extract-cert.c
    popd
  '';

  nativeBuildInputs = [ pkg-config ];

  buildInputs = [ openssl ];

  doCheck = false; # there are no tests for this

  # there is no install target in the kernel for this
  installPhase = ''
    install -d $out/bin $out/usr/share/kernel-module-signing
    install certs/default_x509.genkey $out/usr/share/kernel-module-signing
    install certs/extract-cert $out/bin/
    install scripts/sign-file $out/bin/
    install ${default-keygen}/bin/kernel-module-default-keygen $out/bin/
  '';

  postFixup = ''
    substituteInPlace $out/bin/kernel-module-default-keygen \
      --replace-fail "#CONFIG_PATH#" "$out"

    substituteInPlace $out/usr/share/kernel-module-signing/default_x509.genkey \
      --replace-fail "Build time autogenerated kernel key" "NixOS custom kernel module signing key"
  '';

  meta = with lib; {
    homepage = "https://www.kernel.org/";
    description = "Linux helpers to sign kernel modules";
    maintainers = with maintainers; [ thillux ];
    platforms = platforms.linux;
  };
}
