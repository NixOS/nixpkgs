{ lib, stdenv, fetchurl }:

# Note: this package is used for bootstrapping fetchurl, and thus
# cannot use fetchpatch! All mutable patches (generated by GitHub or
# cgit) that are needed here should be included directly in Nixpkgs as
# files.

stdenv.mkDerivation rec {
  pname = "patchelf";
  version = "0.14.5";

  src = fetchurl {
    url = "https://github.com/NixOS/${pname}/releases/download/${version}/${pname}-${version}.tar.bz2";
    sha256 = "sha256-uaRvKYkyLrifpPYjfiCDbFe0VapDoyVF6gk7Qx2YL1w=";
  };

  strictDeps = true;

  patches =
    # This patch fixes a MIPS-specific bug in patchelf; we want Hydra
    # to generate a bootstrap-files tarball for MIPS that includes
    # this fix.  The patches below can be dropped on the next version bump.
    lib.optionals stdenv.targetPlatform.isMips [
      # https://github.com/NixOS/patchelf/pull/380
      ./patches/380.patch
    ];

  setupHook = [ ./setup-hook.sh ];

  enableParallelBuilding = true;

  # fails 8 out of 24 tests, problems when loading libc.so.6
  doCheck = stdenv.name == "stdenv-linux";

  meta = with lib; {
    homepage = "https://github.com/NixOS/patchelf";
    license = licenses.gpl3Plus;
    description = "A small utility to modify the dynamic linker and RPATH of ELF executables";
    maintainers = [ maintainers.eelco ];
    platforms = platforms.all;
  };
}
