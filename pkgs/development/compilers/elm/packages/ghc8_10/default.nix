{ pkgs, lib }:

self:
pkgs.haskell.packages.ghc810.override {
  overrides =
    self: super:
    let
      inherit (pkgs.haskell.lib.compose) justStaticExecutables overrideCabal doJailbreak;
      elmPkgs = {
        elmi-to-json = justStaticExecutables (
          overrideCabal (drv: {
            version = "unstable-2021-07-19";
            src = pkgs.fetchgit {
              url = "https://github.com/stoeffel/elmi-to-json";
              sha256 = "0vy678k15rzpsn0aly90fb01pxsbqkgf86pa86w0gd94lka8acwl";
              rev = "6a42376ef4b6877e130971faf964578cc096e29b";
              fetchSubmodules = true;
            };

            prePatch = ''
              substituteInPlace package.yaml --replace "- -Werror" ""
              hpack
            '';
            jailbreak = true;

            description = "Tool that reads .elmi files (Elm interface file) generated by the elm compiler";
            homepage = "https://github.com/stoeffel/elmi-to-json";
            license = lib.licenses.bsd3;
            maintainers = [ lib.maintainers.turbomack ];
          }) (self.callPackage ./elmi-to-json { })
        );
      };
    in
    elmPkgs
    // {
      inherit elmPkgs;

      # aeson 2.0.3.0 does not build with attoparsec_0_13_2_5
      aeson = doJailbreak self.aeson_1_5_6_0;
    };
}
