{ lib, stdenv, fetchurl, gettext }:

# Note: this package is used for bootstrapping fetchurl, and thus
# cannot use fetchpatch! All mutable patches (generated by GitHub or
# cgit) that are needed here should be included directly in Nixpkgs as
# files.

stdenv.mkDerivation rec {
  name = "attr-2.4.48";

  src = fetchurl {
    url = "mirror://savannah/attr/${name}.tar.gz";
    sha256 = "1rr4adzwax4bzr2c00f06zcsljv5y6p9wymz1g89ww7cb2rp5bay";
  };

  outputs = [ "bin" "dev" "out" "man" "doc" ];

  nativeBuildInputs = [ gettext ];

  patches = [
    # fix fakechroot: https://github.com/dex4er/fakechroot/issues/57
    ./syscall.patch
  ];

  postPatch = ''
    for script in install-sh include/install-sh; do
      patchShebangs $script
    done
  '';

  meta = with lib; {
    homepage = "https://savannah.nongnu.org/projects/attr/";
    description = "Library and tools for manipulating extended attributes";
    platforms = platforms.unix;
    license = licenses.gpl2Plus;

    # The build failure on Darwin will likely be solved after upgrading the
    # macOS SDK in nixpkgs. Check the current SDK version in
    # ../../../../os-specific/darwin/apple-sdk/default.nix to see if it has
    # been updated to 10.13 or later. Once the requirements are met, building
    # it should be straightforward as Homebrew was able to build it without
    # patching.
    broken = stdenv.isDarwin;
  };
}
