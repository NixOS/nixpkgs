{ stdenv, buildPythonPackage, fetchPypi, pythonOlder
, attrs, click, toml, appdirs, aiohttp, aiohttp-cors
, glibcLocales, typed-ast, pathspec, regex
, setuptools_scm, pytest, mypy-extensions }:

buildPythonPackage rec {
  pname = "black";
  version = "20.8b1";

  disabled = pythonOlder "3.6";

  src = fetchPypi {
    inherit pname version;
    sha256 = "1spv6sldp3mcxr740dh3ywp25lly9s8qlvs946fin44rl1x5a0hw";
  };

  nativeBuildInputs = [ setuptools_scm ];
  checkInputs =  [ pytest glibcLocales ];

  # Necessary for the tests to pass on Darwin with sandbox enabled.
  # Black starts a local server and needs to bind a local address.
  __darwinAllowLocalNetworking = true;

  # Don't know why these tests fails
  # Disable test_expression_diff, because it fails on darwin
  checkPhase = ''
    PATH=$PATH:$out/bin LC_ALL="en_US.UTF-8" pytest \
      --deselect tests/test_black.py::BlackTestCase::test_expression_diff \
      --deselect tests/test_black.py::BlackTestCase::test_cache_multiple_files \
      --deselect tests/test_black.py::BlackTestCase::test_failed_formatting_does_not_get_cached
  '';

  # `test_process_queue` refers to a file thatâ€™s not included in the built package
  prePatch = ''
    cp src/black_primer/primer.json tests/data
  '';
  patches = [ ./tests.patch ];

  propagatedBuildInputs = [ attrs appdirs click toml aiohttp aiohttp-cors pathspec regex typed-ast mypy-extensions ];

  meta = with stdenv.lib; {
    description = "The uncompromising Python code formatter";
    homepage    = "https://github.com/psf/black";
    license     = licenses.mit;
    maintainers = with maintainers; [ sveitser ];
  };

}
