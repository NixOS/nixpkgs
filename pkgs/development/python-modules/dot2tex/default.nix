{ lib
, python
, buildPythonPackage
, fetchFromGitHub
, substituteAll
, pyparsing
, graphviz
, texlive
}:

buildPythonPackage rec {
  pname = "dot2tex";
  version = "2.11.3";

  src = fetchFromGitHub {
     owner = "kjellmf";
     repo = "dot2tex";
     rev = "2.11.3";
     sha256 = "02n73vbz2xafpkl5izmsq9swwzkm0msbnbhz1cgb9qdmh3iba0zg";
  };

  patches = [
    (substituteAll {
      src = ./path.patch;
      inherit graphviz;
    })
    ./test.patch # https://github.com/kjellmf/dot2tex/issues/5
  ];

  propagatedBuildInputs = [ pyparsing ];

  checkInputs = [
    (texlive.combine {
      inherit (texlive) scheme-small preview pstricks;
    })
  ];

  checkPhase = ''
    ${python.interpreter} tests/test_dot2tex.py
  '';

  meta = with lib; {
    description = "Convert graphs generated by Graphviz to LaTeX friendly formats";
    homepage = "https://github.com/kjellmf/dot2tex";
    license = licenses.mit;
  };

}
