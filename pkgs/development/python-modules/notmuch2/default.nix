{ stdenv
, lib
, buildPythonPackage
, notmuch
, python
, cffi
}:

buildPythonPackage {
  pname = "notmuch2";
  inherit (notmuch) version src;

  sourceRoot = "notmuch-${notmuch.version}/bindings/python-cffi";

  nativeBuildInputs = [
    cffi
  ];
  buildInputs = [
    python notmuch cffi
  ];

  # since notmuch 0.35, this package expects _notmuch_config.py that is
  # generated by notmuch's configure script. Substitute our nix-crafted one.
  postPatch = ''
    cat > _notmuch_config.py <<EOF
    NOTMUCH_VERSION_FILE='${notmuch.bindingconfig}/version.txt'
    NOTMUCH_INCLUDE_DIR='${notmuch.out}/include'
    NOTMUCH_LIB_DIR='${notmuch.out}/lib'
    EOF
  '';

  # no tests
  doCheck = false;
  pythonImportsCheck = [ "notmuch2" ];

  meta = with lib; {
    description = "Pythonic bindings for the notmuch mail database using CFFI";
    homepage = "https://notmuchmail.org/";
    license = licenses.gpl3;
    maintainers = with maintainers; [ teto ];
  };
}
