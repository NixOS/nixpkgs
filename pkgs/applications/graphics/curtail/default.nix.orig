{ fetchFromGitHub, meson, ninja, python3, gettext, gtk3, optipng, pngquant
, jpegoptim, libwebp, glib, wrapGAppsHook, gobject-introspection, pango
, appstream-glib, pkg-config, desktop-file-utils }:
python3.pkgs.buildPythonApplication {
  strictDeps = false;
    # ^ This is needed to let gobject-introspection’s setup hook run
    # Disabling this breaks icon themes (at least)
  pname = "curtail";
  version = "1.3.0";
  src = fetchFromGitHub {
    owner = "Huluti";
    repo = "Curtail";
    rev = "master";
    sha256 = "sha256-DqCfhpiIsHbUwe7DsI2RSblV7Pc3PcZwsQiJUzXkQqg=";
  };
    # | The division between native and foreign inputs isn’t 100% clear to me
    # Feel free to clean up, but this package cannot be cross-compiled anyways
  nativeBuildInputs = [
    wrapGAppsHook
    appstream-glib
    desktop-file-utils
    gettext
    meson
    ninja
    pkg-config
  ];
  buildInputs = [
    appstream-glib
    gettext
    glib
    gobject-introspection
    gtk3
    jpegoptim
    libwebp
    optipng
    pango
    pngquant
  ];
  propagatedBuildInputs = [ python3.pkgs.pygobject3 ];
    # ^ Passes this to the Python wrapper
    # Needed so the resulting Python script runs
  format = "other";
    # ^ Tells the Python builder to use normal building methods instead of using a setup.py
  preInstall = ''
    patchShebangs --build /build/source/build-aux/meson/postinstall.py
  '';
}
